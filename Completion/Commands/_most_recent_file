#compdef -k complete-word \C-xm

# Complete the most recent file matching the pattern on the line so
# far: globbing is active, i.e. *.txt will be expanded to the most recent
# file ending in .txt
#
# With a prefix argument, select the Nth most recent matching file;
# negative arguments work in the opposite direction, so for example
# `Esc - \C-x m' gets you the oldest file.
#
# (`Most recent' means most recently modified.)

local file tilde etilde
if [[ $PREFIX = \~*/* ]]; then
  tilde=${PREFIX%%/*}
  etilde=${~tilde}
  file=($~PREFIX*$~SUFFIX(om[${NUMERIC:-1}]N))
  file=(${file/#$etilde/$tilde})
else
  file=($~PREFIX*$~SUFFIX(om[${NUMERIC:-1}]N))
fi
(( $#file )) && compadd -U -i "$IPREFIX" -I "$ISUFFIX" -f -Q $file
